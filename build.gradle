buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

plugins {
    // Publish the generated resources as Maven Artifacts
    // See: https://docs.gradle.org/current/userguide/publishing_setup.html#publishing_overview
    // And: https://docs.gradle.org/current/userguide/dependency_management_terminology.html#sub:terminology_component
    id 'maven-publish'

    // Push the artifact to bintray
    // See: https://github.com/bintray/gradle-bintray-plugin
    id 'com.jfrog.bintray' version '1.8.5'
}

// Retrieve local context
def localProperties = new Properties();
localProperties.load(new FileInputStream(rootProject.file("local.properties")));

// Expose custom properties to all modules in the project.
ext {
    version = '0.1.0'
    githubUrl = 'https://github.com/bsinou/cells-android-client.git'

    // Centralize dependency version
    cellsSdkJavaLibVersion = "0.1.3-SNAPSHOT"

    // Android specific constraints
    minSdkVersion = 21
    compileSdkVersion = 29
    targetSdkVersion = 29
    buildToolsVersion = "29.0.3"

    // Credential management
    bintrayUser = localProperties['bintray.user']
    bintrayKey = localProperties['bintray.key']
}

allprojects {

    repositories {

        // Typically useful when working on the Java SDK so that we can use updated code without publishing to bintray.
        mavenLocal()

        // Using both google and jcenter to resolve dependencies.
        google()
        jcenter()

        // Also Register Pydio own repository on bintray
        maven {
            url "https://dl.bintray.com/pydio/cells"
        }
    }
}
