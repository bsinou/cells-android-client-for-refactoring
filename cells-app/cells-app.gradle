apply plugin: 'com.android.application'

// Module main parameters 
def appID = "com.pydio.android.Client"
def appName = "Cells"

// Generate static files from templates 
def generateStaticFiles (Properties versionProperties, String basePath, appName) {
        
    def versionName = versionProperties['version.name']
    def versionCode = versionProperties['version.code']
    def timestamp = new Date().format("yyyy.MM.dd")
    def year = new Date().format("yyyy")
    def templatePath = basePath + "/src/templates/about.html"
    def targetPath = basePath + "/src/main/assets/about.html";
    
    def body = new File(templatePath).getText('UTF-8')
    body = body.replaceAll("##APP_NAME##", appName)
    body = body.replaceAll("##VERSION_NAME##", versionName)
    body = body.replaceAll("##VERSION_CODE##", versionCode)
    body = body.replaceAll("##VERSION_DATE##", timestamp)
    body = body.replaceAll("##VERSION_YEAR##", year)
    def aboutFile = new File(targetPath)
    aboutFile.write(body)
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    // Retrieve version info
    // TODO make various paths Windows compliant 
    def basePath =  "${project.projectDir}"
    def versionProperties = new Properties();
    versionProperties.load(new FileInputStream(basePath + "/version.properties"));
    def vName = versionProperties['version.name']
    def apkName = appName + "-" + vName + ".apk"
    generateStaticFiles(versionProperties, basePath, appName)

    defaultConfig {
        applicationId appID
        versionName vName
        versionCode versionProperties['version.code'] as int

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant ->
                    variant.outputs.all {
                        outputFileName = apkName
                    }
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant ->
                    variant.outputs.all {
                        outputFileName = apkName
                    }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO solve errors and get rid of this
    lintOptions {
        abortOnError false
    }

// TODO Double check this and re-eneable if necessary
//   packagingOptions {
//             exclude 'META-INF/DEPENDENCIES'
//             exclude 'META-INF/LICENSE'
//             exclude 'META-INF/LICENSE.txt'
//             exclude 'META-INF/license.txt'
//             exclude 'META-INF/NOTICE'
//             exclude 'META-INF/NOTICE.txt'
//             exclude 'META-INF/notice.txt'
//             exclude 'META-INF/ASL2.0'
//         }

}


dependencies {

    // local external dependencies from libs subfolder, unused
    // implementation fileTree(dir: "libs", include: ["*.jar"])

    // Cells: the Java SDK and the Android Client
    implementation project(":sdk")
    implementation "com.pydio.cells:cells-sdk-java:${rootProject.ext.cellsSdkJavaLibVersion}"

    // AndroidX and JetPack to ship version 29+ code to older devices
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    // For the tests
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

